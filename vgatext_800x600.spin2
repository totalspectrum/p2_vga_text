''
'' clock frequency settings
'' for 800x600 we use a 40 MHz pixel clock
'' which x4 gives a 160 MHz system clock
'' or x5 gives a 200 MHz system clock
'' those are probably good choices for running
'' with this
''
OBJ
  vt: "timing800x600"
  
CON
  CELL_SIZE = 4  ' bytes per character: use 4 for 8bpp colors, 8 for 24bpp colors

  pixel_clock_freq = vt.pixel_clock_freq
  FONT_WIDTH = 8
  FONT_HEIGHT = (vt.PIXEL_WIDTH==600) ? 15 : 16
  
  COLS = vt.PIXEL_WIDTH / FONT_WIDTH
  ROWS = vt.PIXEL_HEIGHT / FONT_HEIGHT

DAT
'
' font buffer
'
	long
fontdata
	file "unscii-16.bin"

VAR
    long params[40]
    byte screen_buffer[COLS*ROWS*CELL_SIZE]

OBJ
    vga: "vga_tile_driver.spin2"

PUB start(pinbase) | i, pclkscale, pclk, sysclk, x, fontptr
  ' calculate clock frequency
  pclk := pixel_clock_freq
  sysclk := clkfreq  ' system clock
  ' calculate scale := $8000_0000 * pclk / sysclk

  pclkscale := calcscale(pclk, sysclk)

  fontptr := @fontdata
  if FONT_HEIGHT == 15
    fontptr += 256 * FONT_WIDTH / 8  ' skip a row

  i := 0
  params[00] := pinbase
  params[01] := @screen_buffer	' screen buffer
  params[02] := COLS           ' screen columns
  params[03] := ROWS           ' screen rows
  params[04] := fontptr	' font data: skip first row
  params[05] := FONT_WIDTH	' font width
  params[06] := FONT_HEIGHT    ' font height
  params[07] := pclkscale 	' pixel clock scaling value
  params[08] := 40           	' horizontal front porch
  params[09] := 128            ' hsync pulse
  params[10] := 88        	' horizontal back porch
  params[11] := 1              ' vertical front porch
  params[12] := 4              ' vertical sync lines
  params[13] := 23             ' vertical back porch
  params[14] := %00            ' vertical/horizontal polarity
  params[15] := CELL_SIZE
  x := vga.start(@params)
  init_terminal()
  return x

PUB stop()
  vga.stop()
  
#include "vga_text_routines.spinh"
#include "std_text_routines.spinh"

