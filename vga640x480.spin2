''
'' clock frequency settings
'' for 640x480 @ 73 Hz we use a 31.5 MHz pixel clock
'' which x4 gives a 126 MHz system clock
'' we can get that by dividing 20_000_000 by 10
'' then multiplying by 63
'' then dividing by 1 again
CON
  _XTALFREQ     = 20_000_000                                    ' crystal frequency
  _XDIV         = 10                                            ' crystal divider to give 1MHz
  _XMUL         = 63                                          ' crystal / div * mul
  _XDIVP        = 1                                             ' crystal / div * mul /divp to give _CLKFREQ (1,2,4..30)
  _XOSC         = %10                                  'OSC    ' %00=OFF, %01=OSC, %10=15pF, %11=30pF
  _XSEL         = %11                                   'XI+PLL ' %00=rcfast(20+MHz), %01=rcslow(~20KHz), %10=XI(5ms), %11=XI+PLL(10ms)
  _XPPPP        = ((_XDIVP>>1) + 15) & $F                       ' 1->15, 2->0, 4->1, 6->2...30->14
  _CLOCKFREQ    = _XTALFREQ / _XDIV * _XMUL / _XDIVP            ' internal clock frequency                
  _SETFREQ      = 1<<24 + (_XDIV-1)<<18 + (_XMUL-1)<<8 + _XPPPP<<4 + _XOSC<<2  ' %0000_000e_dddddd_mmmmmmmmmm_pppp_cc_00  ' setup  oscillator

  sys_clock_freq = _CLOCKFREQ
  sys_clock_mode = _SETFREQ
  pixel_clock_freq = 31_500_000
  
CON
	COLS = 80
	ROWS = 30
        FONT_HEIGHT = 16
	
DAT
'
'
' Bitmap
'
        orgh
buffer
	long 0[COLS*ROWS*2]
'
' font buffer
'
	long
fontdata
	file "unscii-16.bin"
'	file "unscii-8-fantasy.bin"

'=================================================================================================================
' BELOW HERE IS THE DEMO CODE
'=================================================================================================================
democolors
    long $FF000000, $FFFF0000, $00FF0000, $00FFFF00
    long $0000FF00, $FF00FF00, $FFFFFF00, $00000000
    long $7F000000, $007F7F00, $007F0000

VAR
    long params[40] ' parameters for running the VGA tile driver

OBJ
    vga: "vga_tile_driver.spin2"

PUB demo | x, y, fgcol, bgcol, ch, grey, col1, col2, idx
    clkset(sys_clock_mode, sys_clock_freq)  ' 20 MHz crystal * 8

    initparams
    vga.start(@params)
    ch := 0
    longfill(@buffer, 0, 80*30*2)
    pausems(1000)
    cls

    repeat y from 0 to ROWS-1
        grey := y<<3
        bgcol := (grey<<24) | (grey<<16) | (grey<<8)
        repeat x from 0 to COLS-1
          grey := (x & 15)
	  idx := x / 16
          col1 := democolors[idx]
	  col2 := democolors[idx+1]
	  fgcol := colorblend(col1, col2, (grey<<4) + grey)
          glyphat(x, y, ch++, fgcol, bgcol)
    runtext

'
' calculate ($8000_0000 * a) / (b)
'
CON PIXSHIFT = 31

PRI calcscale(a, b) | shiftcnt
  shiftcnt := PIXSHIFT
  ' remove factors of 5 (will be pretty common)
  repeat while 0 == (a // 5) and 0 == (b // 5)
    a := a / 5
    b := b / 5

  ' scale a up as much as we can
  repeat while ((a & $4000_0000) == 0) and shiftcnt > 0
    a := a << 1
    shiftcnt--
  repeat while ((b & 1) == 0) and shiftcnt > 0
    b := b>>1
    shiftcnt--
  return (a / b) << shiftcnt
      
'
' calculate ($8000_0000 * a) / (b)
'
PUB initparams | i, pclkscale, pclk, sysclk, x
  ' calculate clock frequency
  pclk := pixel_clock_freq ' pixel clock
  sysclk := clkfreq  ' system clock
  ' calculate scale := $8000_0000 * pclk / sysclk
  ' this is equal to pclk / (sysclk / $8000_000)
  pclkscale := calcscale(pclk, sysclk)

  i := 0
  params[i++] := @buffer	' screen buffer
  params[i++] := COLS           ' screen columns
  params[i++] := ROWS           ' screen rows
  params[i++] := @fontdata	' font data
  params[i++] := 8		' font width
  params[i++] := FONT_HEIGHT    ' font height
  params[i++] := pclkscale 'fset           ' pixel clock scaling value
  params[i++] := 24             ' horizontal front porch
  params[i++] := 40             ' hsync pulse
  params[i++] := 128            ' horizontal back porch
  params[i++] := 9             ' vertical front porch
  params[i++] := 2              ' vertical sync lines
  params[i++] := 29             ' vertical back porch
  
PUB colorblend(a, b, mix)
  asm
    setpiv mix
    blnpix a, b
  endasm
  return a

PUB cls
   longfill(@buffer, 0, COLS*ROWS*2)

PUB glyphat(x, y, ch, fgcol, bgcol) | bufptr
  bufptr := @buffer
  bufptr += (y*COLS + x) *8
  fgcol |= (ch & $FF)
  long[bufptr] := fgcol
  bufptr += 4
  long[bufptr] := bgcol

#include "vga_text_routines.spinh"
#include "std_text_routines.spinh"

PUB runtext | n
  pausems(1000)
  init_terminal
  n := 0
  repeat
    nl
    str(string("Hello! ", 27, "[BCursor down"))
    str(string(27, "[31mRed text "))
    str(string(27, "[1;31mBright Red text"))
    str(string(27, "[7mInverse "))
    str(string(27, "[22;31mBold off "))
    str(string(27, "[0mEffects off "))
    dec(n)
    n++
    
    nl
    pausems(2000)
    str(string(27, "[1;1H"))
    str(string(27, "[0J"))
    str(@teststring)
    pausems(10000)

DAT
teststring
  FILE "picture.ans"
  long 0
