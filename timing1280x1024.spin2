{{
  This file contains various constants and utilities for
  setting up VGA for 1280x1024
  Note that this requires a pixel clock of 108 MHz MHz, so for
  CELL_SIZE=1 we need at least 216 MHz system clock, and for
  CELL_SIZE=8 we need 324 MHz. Other CELL_SIZEs are not practical.
}}

CON
  pixel_clock_freq = 108_000_000
  PIXEL_WIDTH = 1280
  PIXEL_HEIGHT =1024

{{
   set up VGA parameters in a parameter block
   p points to the parameter block, which will be
   set to contain:
   p[0]: pixel clock scaling value
   p[1]: horizontal front porch (in pixels)
   p[2]: hsync pulse (in pixels)
   p[3]: horizontal back porch (in pixels)
   p[4]: vertical front porch (in lines)
   p[5]: vertical sync lines
   p[6]: vertical back porch (in lines)
   p[7]: vertical/horizonal polarity
}}

PUB gettiming(p) | pclkscale, pclk, sysclk
  ' calculate clock frequency
  pclk := pixel_clock_freq
  sysclk := clkfreq  ' system clock
  ' calculate scale := $8000_0000 * pclk / sysclk
  pclkscale := calcscale(pclk, sysclk)

  long[p][0] := pclkscale      ' pixel clock scaling value
  long[p][1] := 48             ' horizontal front porch
  long[p][2] := 112            ' hsync pulse
  long[p][3] := 248            ' horizontal back porch
  long[p][4] := 1              ' vertical front porch
  long[p][5] := 3              ' vertical sync lines
  long[p][6] := 38             ' vertical back porch
  long[p][7] := %00            ' vertical/horizontal polarity (pos/neg)

'
' calcscale(a, b)
'   calculate ($8000_0000 * a) / (b)
'   we want to do this carefully so as to be as accurate as
'   possible, so this code is a bit complicated
'
CON PIXSHIFT = 31

PRI calcscale(a, b) : shiftcnt
  shiftcnt := PIXSHIFT
  ' remove factors of 5 (will be pretty common)
  repeat while 0 == (a // 5) and 0 == (b // 5)
    a := a / 5
    b := b / 5

  ' scale a up as much as we can
  repeat while ((a & $4000_0000) == 0) and shiftcnt > 0
    a := a << 1
    shiftcnt--
  repeat while ((b & 1) == 0) and shiftcnt > 0
    b := b>>1
    shiftcnt--
  shiftcnt := (a / b) << shiftcnt
